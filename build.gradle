plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.165'
}

version = mod_version
group = mod_group_id

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://cursemaven.com' }
    maven { url = 'https://maven.architectury.dev/' }
    maven { url = 'https://maven.neoforged.net/releases/' }
    maven { url = 'https://maven.impactdev.net/repository/development/' }
    maven {
        name = 'Nucleoid'
        url = 'https://maven.nucleoid.xyz/'
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
        argument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    data {
        arguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    // Cobblemon
    compileOnly "com.cobblemon:neoforge:${cobblemon_version}"

    // GooeyLibs API (compile-time only; full mod required at runtime)
    compileOnly "ca.landonjw.gooeylibs:api:${gooeylibs_version}"

    // CobbleLib NeoForge (local build)
    compileOnly files('../CobbleLib-NeoForge/build/libs/cobblelib-1.0.0.jar')

    // exp4j - math expression library (bundled via jarJar)
    implementation 'net.objecthunter:exp4j:0.4.8'
    jarJar(group: 'net.objecthunter', name: 'exp4j', version: '[0.4.8,1.0)') {
        jarJar.pin(it, '0.4.8')
    }

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version         : neo_version,
            loader_version_range: loader_version_range,
            mod_id              : mod_id,
            mod_name            : mod_name,
            mod_license         : mod_license,
            mod_version         : mod_version,
            mod_authors         : mod_authors,
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// Enable jarJar to bundle dependencies
tasks.named('jarJar').configure {
    archiveClassifier = ''
}

tasks.named('jar').configure {
    archiveClassifier = 'slim'
}
